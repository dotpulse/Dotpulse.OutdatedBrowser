prototype(Dotpulse.OutdatedBrowser:CSS) <  prototype(Neos.Fusion:Case) {
    configuration = ${Configuration.setting('Dotpulse.OutdatedBrowser.css')}
    path = 'resource://Dotpulse.OutdatedBrowser/Public/Main.css'

    @context.props = ${this}
    @ignoreProperties = ${['configuration','path']}

    inline {
        condition = ${props.configuration.inline}
        renderer = Neos.Fusion:Tag {
            tagName = 'style'
            content = Carbon.FileContent:FileContent {
                path = ${props.path}
            }
        }
    }
    external {
        condition = true
        renderer = Neos.Fusion:Tag {
            tagName = 'link'
            attributes {
                rel = 'stylesheet'
                href = Neos.Fusion:Array {
                    path= Neos.Fusion:ResourceUri {
                        path = ${props.path}
                    }
                    arg = '?h='
                    hash = Carbon.FileContent:HashOfFile {
                        path = ${props.path}
                    }
                }
            }

        }
    }
}
