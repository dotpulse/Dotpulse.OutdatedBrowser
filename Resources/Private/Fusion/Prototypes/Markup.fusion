prototype(Dotpulse.OutdatedBrowser:Markup) <  prototype(Neos.Fusion:Tag) {
    language = Neos.Fusion:Case {
        @context {
            availableLanguages = ${['cs','da','de','el','en','es-pe','es','et','fa','fi','fr','hr','hu','id','it','ja','ko','lt','nb','nl','pl','pt-br','pt','ro','ru','sk','sl','sv','tr','uk','zh-cn','zh-tw']}
            documentLanguage = Neos.Fusion:Case {
                hasDimension {
                    condition = ${Configuration.setting('Neos.ContentRepository.contentDimensions.language') != null}
                    renderer = ${String.replace(documentNode.context.dimensions.language[0], '_', '-')}
                }
                hasDefaultLocale {
                    condition = ${String.crop(Configuration.setting('Neos.Flow.i18n.defaultLocale'), 2)}
                    renderer = ${String.crop(Configuration.setting('Neos.Flow.i18n.defaultLocale'), 2)}
                }
                fallbackLanguage {
                    condition = ${configuration.fallbackLanguage}
                    renderer = ${configuration.fallbackLanguage}
                }
                default {
                    condition = true
                    renderer = 'en'
                }
            }
            fallbackLanguage = Neos.Fusion:Case {
                fallbackLanguage {
                    condition = ${configuration.fallbackLanguage}
                    renderer = ${configuration.fallbackLanguage}
                }
                default {
                    condition = true
                    renderer = 'en'
                }
            }
        }
        hasFile {
            condition = ${Array.indexOf(availableLanguages, documentLanguage) != -1}
            renderer = ${documentLanguage}
        }
        noFile {
            condition = ${Array.indexOf(availableLanguages, fallbackLanguage) != -1}
            renderer = ${fallbackLanguage}
        }
        fallbackToEn {
            condition = true
            renderer = 'en'
        }
    }

    @context {
        configuration = ${Configuration.setting('Dotpulse.OutdatedBrowser')}
        language = ${this.language}
    }

    tagName = 'div'
    attributes {
        id = 'outdated'
        data-lowerthan = Neos.Fusion:Case {
            ie8 {
                condition = ${configuration.lowerThan == 'IE8'}
                renderer ='borderSpacing'
            }
            ie9 {
                condition = ${configuration.lowerThan == 'IE9'}
                renderer ='boxShadow'
            }
            ie10 {
                condition = ${configuration.lowerThan == 'IE10'}
                renderer ='transform'
            }
            ie11 {
                condition = ${configuration.lowerThan == 'IE11'}
                renderer ='borderImage'
            }
            edge {
                condition = ${configuration.lowerThan == 'Edge'}
                renderer ='js:Promise'
            }
            flexbox {
                condition = ${configuration.lowerThan == 'Flexbox'}
                renderer ='flex-wrap'
            }
            oldGrid {
                condition = ${configuration.lowerThan == 'oldGrid'}
                renderer ='grid-row'
            }
            grid {
                condition = ${configuration.lowerThan == 'Grid'}
                renderer ='grid-auto-flow'
            }
            a23 {
                condition = ${configuration.lowerThan == 'A23'}
                renderer ='writing-mode'
            }
            cssProperty {
                condition = true
                renderer = ${configuration.lowerThan}
            }
        }
    }

    content = Neos.Fusion:Array {
        CSS = Dotpulse.OutdatedBrowser:CSS {
            @if.include = ${Configuration.setting('Dotpulse.OutdatedBrowser.css.include')}
        }
        Markup = Neos.Fusion:Template {
            templatePath = ${'resource://Dotpulse.OutdatedBrowser/Private/Templates/' + language + '.html'}
            // Change target
            @process.target = ${configuration.target ? String.replace(value, 'id="btnUpdateBrowser"', 'id="btnUpdateBrowser" rel="noopener" target="' + configuration.target + '"') : value}
        }
        Javascript = Dotpulse.OutdatedBrowser:Javascript {
            @if.include = ${Configuration.setting('Dotpulse.OutdatedBrowser.javascript.include')}
        }
    }
}
