prototype(Dotpulse.OutdatedBrowser:Markup) <  prototype(Neos.Fusion:Tag) {
    language = Neos.Fusion:Case {
        @context {
            availableLanguages = ${['cs','da','de','el','en','es-pe','es','et','fa','fi','fr','hr','hu','id','it','ja','ko','lt','nb','nl','pl','pt-br','pt','ro','ru','sk','sl','sv','tr','uk','zh-cn','zh-tw']}
            documentLanguage = Neos.Fusion:Case {
                hasDimension {
                    condition = ${Configuration.setting('Neos.ContentRepository.contentDimensions.language') != null}
                    renderer = ${String.replace(documentNode.context.dimensions.language[0], '_', '-')}
                }
                hasDefaultLocale {
                    condition = ${String.crop(Configuration.setting('Neos.Flow.i18n.defaultLocale'), 2)}
                    renderer = ${String.crop(Configuration.setting('Neos.Flow.i18n.defaultLocale'), 2)}
                }
                fallbackLanguage {
                    condition = ${Configuration.setting('Dotpulse.OutdatedBrowser.fallbackLanguage')}
                    renderer = ${Configuration.setting('Dotpulse.OutdatedBrowser.fallbackLanguage')}
                }
                default {
                    condition = true
                    renderer = 'en'
                }
            }
            fallbackLanguage = Neos.Fusion:Case {
                fallbackLanguage {
                    condition = ${Configuration.setting('Dotpulse.OutdatedBrowser.fallbackLanguage')}
                    renderer = ${Configuration.setting('Dotpulse.OutdatedBrowser.fallbackLanguage')}
                }
                default {
                    condition = true
                    renderer = 'en'
                }
            }
        }
        hasFile {
            condition = ${Array.indexOf(availableLanguages, documentLanguage) != -1}
            renderer = ${documentLanguage}
        }
        noFile {
            condition = ${Array.indexOf(availableLanguages, fallbackLanguage) != -1}
            renderer = ${fallbackLanguage}
        }
        fallbackToEn {
            condition = true
            renderer = 'en'
        }
    }
    lowerThan = ${Configuration.setting('Dotpulse.OutdatedBrowser.lowerThan')}
    target = ${Configuration.setting('Dotpulse.OutdatedBrowser.target')}

    @context {
        lowerThan = ${this.lowerThan}
        target = ${this.target}
        language = ${this.language}
    }

    tagName = 'div'
    attributes {
        id = 'outdated'
        data-lowerthan = Neos.Fusion:Case {
            ie8 {
                condition = ${lowerThan == 'IE8'}
                renderer ='borderSpacing'
            }
            ie9 {
                condition = ${lowerThan == 'IE9'}
                renderer ='boxShadow'
            }
            ie10 {
                condition = ${lowerThan == 'IE10'}
                renderer ='transform'
            }
            ie11 {
                condition = ${lowerThan == 'IE11'}
                renderer ='borderImage'
            }
            edge {
                condition = ${lowerThan == 'Edge'}
                renderer ='js:Promise'
            }
            flexbox {
                condition = ${lowerThan == 'Flexbox'}
                renderer ='flex-wrap'
            }
            oldGrid {
                condition = ${lowerThan == 'oldGrid'}
                renderer ='grid-row'
            }
            grid {
                condition = ${lowerThan == 'Grid'}
                renderer ='grid-auto-flow'
            }
            a23 {
                condition = ${lowerThan == 'A23'}
                renderer ='writing-mode'
            }
            cssProperty {
                condition = true
                renderer = ${lowerThan}
            }
        }
    }

    content = Neos.Fusion:Array {
        CSS = Dotpulse.OutdatedBrowser:CSS {
            @if.include = ${Configuration.setting('Dotpulse.OutdatedBrowser.css.include')}
        }
        Markup = Neos.Fusion:Template {
            templatePath = ${'resource://Dotpulse.OutdatedBrowser/Private/Templates/' + language + '.html'}
            // Change target
            @process.target = ${target ? String.replace(value, 'id="btnUpdateBrowser"', 'id="btnUpdateBrowser" rel="noopener" target="' + target + '"') : value}
        }
        Javascript = Dotpulse.OutdatedBrowser:Javascript {
            @if.include = ${Configuration.setting('Dotpulse.OutdatedBrowser.javascript.include')}
        }
    }
}
